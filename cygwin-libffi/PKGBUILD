pkgname=cygwin-libffi
pkgver=3.4.6
pkgrel=1
pkgdesc="Portable foreign function interface library (Cygwin)"
arch=('any')
url="https://sourceware.org/libffi/"
license=('MIT')
groups=('cygwin-toolchain')
depends=('cygwin')
makedepends=('cygwin-configure')
options=('!strip' 'staticlibs' '!buildflags')
source=("https://github.com/libffi/libffi/releases/download/v${pkgver}/libffi-${pkgver}.tar.gz")
sha256sums=('b0dea9df23c863a7a50e825440f3ebffabd65df1497108e5d437747843895a4e')

_targets="x86_64-pc-cygwin"

build() {
  cd "$srcdir"/libffi-${pkgver}
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    mkdir -p build-${_target} && pushd build-${_target}
      ${_target}-configure
      make
    popd
  done
}

package() {
  cd "$srcdir"/libffi-${pkgver}
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    (cd build-${_target} && make DESTDIR="$pkgdir" install)
    find "$pkgdir"/usr/${_target} -name '*.dll' -exec ${_target}-strip --strip-unneeded {} \;
    find "$pkgdir"/usr/${_target} -name '*.a' -o -name '*.dll' | xargs ${_target}-strip -g
    # remove unnecessary files
    rm -r "$pkgdir"/usr/${_target}/share/man/
    # remove unwanted files
    rm "$pkgdir"/usr/${_target}/share/info/dir
  done
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/${pkgname}
}
