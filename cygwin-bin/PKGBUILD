_realname=cygwin
pkgname=cygwin-bin
pkgver=3.4.10
pkgrel=1
pkgdesc="POSIX compatibility C runtime for Windows (bootstrap)"
arch=('any')
url="https://cygwin.com/"
_mirror="https://mirrors.kernel.org/sourceware/cygwin"
license=('GPL')
provides=('cygwin')
conflicts=('cygwin')
options=('!strip' 'staticlibs' '!emptydirs')
source=("${_realname}-${pkgver}-1-x86_64.tar.xz::${_mirror}/x86_64/release/${_realname}/${_realname}-${pkgver}-1.tar.xz"
        "${_realname}-devel-${pkgver}-1-x86_64.tar.xz::${_mirror}/x86_64/release/${_realname}/${_realname}-devel/${_realname}-devel-${pkgver}-1.tar.xz")
noextract=("${source[@]%%::*}")
sha256sums=('bb81511484de5e0182b0baefad1bd1abee618fa0656a25b283b0a32ff72ac088'
            'fabe654bbc602e10c3240ac3fb8edc2f558e29019bcc08249f153a91b935ae13')

_targets="x86_64-pc-cygwin"

package() {
  for _target in ${_targets}; do
    echo "Extracting ${_target} tarballs"
    mkdir -p "$pkgdir"/usr/${_target}
    tar -x -v -f "${_realname}-${pkgver}-1-${_target::-10}.tar.xz" -C "$pkgdir"/usr/${_target} --strip-components=1 --wildcards usr/\*
    tar -x -v -f "${_realname}-devel-${pkgver}-1-${_target::-10}.tar.xz" -C "$pkgdir"/usr/${_target} --strip-components=1 --wildcards usr/\*
    # install licenses
    if [[ ! -f "$pkgdir"/usr/share/licenses/${pkgname}/COPYING ]]; then
      install -Dm644 "$pkgdir"/usr/${_target}/share/doc/Cygwin/COPYING \
          "$pkgdir"/usr/share/licenses/${pkgname}/COPYING
    fi
    if [[ ! -f "$pkgdir"/usr/share/licenses/${pkgname}/CYGWIN_LICENSE ]]; then
      install -Dm644 "$pkgdir"/usr/${_target}/share/doc/Cygwin/CYGWIN_LICENSE \
          "$pkgdir"/usr/share/licenses/${pkgname}/CYGWIN_LICENSE
    fi
    # remove unnecessary files
    rm -rf "$pkgdir"/usr/${_target}/share/doc/
    rm -rf "$pkgdir"/usr/${_target}/share/cygwin/
    find "$pkgdir"/usr/${_target}/bin/ -type f ! -name \*.dll -delete
    # fixup file modes
    find "$pkgdir"/usr/${_target}/ -mindepth 1 -type d -perm 775 -exec chmod 755 {} +
  done
  # cleanup exes
  find "$pkgdir" -type f -name \*.exe -delete
}
