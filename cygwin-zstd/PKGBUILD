pkgname=cygwin-zstd
pkgver=1.5.5
pkgrel=1
pkgdesc="Zstandard - Fast real-time compression algorithm (Cygwin)"
arch=('any')
url="https://facebook.github.io/zstd/"
license=('BSD' 'GPL2')
groups=('cygwin-toolchain')
depends=('cygwin')
makedepends=('cygwin-make')
options=('!strip' 'staticlibs' '!buildflags')
source=("https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.zst"
        cygming-config.patch
        cygwin-win32.patch)
sha256sums=('ce264bca60eb2f0e99e4508cffd0d4d19dd362e84244d7fc941e79fa69ccf673'
            '2204cb4b4864e4299a99740d9892414c3613cb74cdbb5ec851016295c1cf2d7b'
            'ef4cb21f3e27b1ff7e47108e287ae9f83692d77bea01c2f553310e7d16760e1e')

_targets="x86_64-pc-cygwin"

prepare() {
  cd "$srcdir"/zstd-${pkgver}

  patch -Np2 -i ../cygming-config.patch
  patch -Np2 -i ../cygwin-win32.patch
}

build() {
  export CFLAGS+=' -ffat-lto-objects'

  for _target in ${_targets}; do
    echo "Building for ${_target}"
    rm -rf build-${_target}
    cp -r zstd-${pkgver} build-${_target}
    pushd build-${_target}
      ${_target}-make -C lib ZSTD_LEGACY_SUPPORT=0 ZSTD_LIB_DEPRECATED=0 lib-mt \
        -j 1 PREFIX=/usr/${_target} OS=Cygwin EXT=.exe
    popd
  done
}

package() {
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    pushd build-${_target}
      make DESTDIR="$pkgdir" install -C lib ZSTD_LEGACY_SUPPORT=0 ZSTD_LIB_DEPRECATED=0 \
        -j 1 PREFIX=/usr/${_target} OS=Cygwin EXT=.exe
    popd
    ${_target}-strip --strip-unneeded "$pkgdir"/usr/${_target}/bin/*.dll
    ${_target}-strip -g "$pkgdir"/usr/${_target}/lib/*.a
  done
  install -Dm 644 "$srcdir"/zstd-${pkgver}/LICENSE -t "$pkgdir"/usr/share/licenses/${pkgname}
}
