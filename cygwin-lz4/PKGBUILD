pkgname=cygwin-lz4
pkgver=1.9.4
pkgrel=1
pkgdesc="Extremely fast compression algorithm (Cygwin)"
arch=('any')
url="https://lz4.github.io/lz4/"
license=('BSD')
groups=('cygwin-toolchain')
depends=('cygwin')
makedepends=('cygwin-make')
options=('!strip' 'staticlibs' '!buildflags')
source=("https://github.com/lz4/lz4/archive/v${pkgver}/lz4-v${pkgver}.tar.gz"
        cygming-dll-phony.patch)
sha256sums=('0b0e3aa07c8c063ddf40b082bdf7e37a1562bda40a0ff5272957f3e987e0e54b'
            '1003288780431067b40fac5714d6ecec0d5e9ca80be7b6ae69ca76266d2507ff')

_targets="x86_64-pc-cygwin"

prepare() {
  cd "$srcdir"/lz4-${pkgver}

  patch -Np2 -i ../cygming-dll-phony.patch
}

build() {
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    rm -rf build-${_target}
    cp -r lz4-${pkgver} build-${_target}
    pushd build-${_target}
      ${_target}-make WINDRES=${_target}-windres -C lib \
        TARGET_OS=CYGWIN PREFIX=/usr/${_target}
    popd
  done
}

package() {
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    pushd build-${_target}
      make DESTDIR="$pkgdir" install -C lib \
        TARGET_OS=CYGWIN PREFIX=/usr/${_target}
    popd
    ${_target}-strip --strip-unneeded "$pkgdir"/usr/${_target}/bin/*.dll
    ${_target}-strip --strip-debug "$pkgdir"/usr/${_target}/lib/*.a
  done
  install -Dm 644 "$srcdir"/lz4-${pkgver}/lib/LICENSE -t "$pkgdir"/usr/share/licenses/${pkgname}
}
