pkgname=cygwin-readline
_basever=8.2
_patchlevel=001
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc="GNU readline library (Cygwin)"
arch=('any')
url="https://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL-3.0-only')
groups=('cygwin-toolchain')
depends=('cygwin' 'cygwin-ncurses')
makedepends=('cygwin-configure')
options=('!strip' 'staticlibs' '!buildflags')
source=("https://ftp.gnu.org/gnu/readline/readline-${_basever}.tar.gz")
if [ ${_patchlevel} -gt 00 ]; then
    for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
        source=(${source[@]} "https://ftp.gnu.org/gnu/readline/readline-${_basever}-patches/readline${_basever//./}-$(printf "%03d" ${_p})")
    done
fi
sha256sums=('3feb7171f16a84ee82ca18a36d7b9be109a52c04f492a053331d7d1095007c35'
            'bbf97f1ec40a929edab5aa81998c1e2ef435436c597754916e6a5868f273aff7')

_targets="x86_64-pc-cygwin"

prepare() {
  cd "$srcdir"/readline-${_basever}

  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" ${_p})"
    patch -Np0 -i "$srcdir"/readline${_basever//./}-$(printf "%03d" ${_p})
  done

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
  export DLLVERSION=7

  cd "$srcdir"/readline-${_basever}
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    mkdir -p build-${_target} && pushd build-${_target}
      ${_target}-configure \
        --enable-multibyte \
        --with-curses
      make
    popd
  done
}

package() {
  cd "$srcdir"/readline-${_basever}
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    (cd build-${_target} && make DESTDIR="$pkgdir" install)
    ${_target}-strip --strip-unneeded "$pkgdir"/usr/${_target}/bin/*.dll
    ${_target}-strip --strip-debug "$pkgdir"/usr/${_target}/lib/*.a
    # remove unnecessary files
    rm -r "$pkgdir"/usr/${_target}/share/
  done
  install -Dm644 "$srcdir"/readline-${_basever}/COPYING "$pkgdir"/usr/share/licenses/${pkgname}/COPYING
}
