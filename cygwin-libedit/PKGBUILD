pkgname=cygwin-libedit
_pkgver=20221030-3.1
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc="Command line editor library providing generic line editing, history, and tokenization functions (Cygwin)"
arch=('any')
url="https://thrysoee.dk/editline/"
license=('BSD')
groups=('cygwin-toolchain')
depends=('cygwin' 'cygwin-ncurses')
makedepends=('cygwin-configure')
options=('!strip' 'staticlibs' '!buildflags' '!emptydirs')
source=("${url}/libedit-${_pkgver}.tar.gz"
        cygmsys-display-array-wchar.patch
        cygwin-config.patch)
sha256sums=('f0925a5adf4b1bf116ee19766b7daa766917aec198747943b1c4edf67a4be2bb'
            'f4ecbd871967a53ec063558959e706970171a1f1096f0c56494d0af3b46e4a04'
            'b04f81f22bd5601c3df54dcdc7f79c5a25a7a8563336238129ac7bba123ed2a3')

_targets="x86_64-pc-cygwin"

prepare() {
  cd "$srcdir"/libedit-${_pkgver}

  patch -Np1 -i ../cygmsys-display-array-wchar.patch
  patch -Np1 -i ../cygwin-config.patch
}

build() {
  cd "$srcdir"/libedit-${_pkgver}
  for _target in ${_targets}; do
    echo "Building for ${_target}"
    mkdir -p build-${_target} && pushd build-${_target}
      ${_target}-configure
      make
    popd
  done
}

package() {
  cd "$srcdir"/libedit-${_pkgver}
  for _target in ${_targets}; do
    echo "Installing for ${_target}"
    (cd build-${_target} && make DESTDIR="$pkgdir" install)
    find "$pkgdir"/usr/${_target} -name '*.dll' -exec ${_target}-strip --strip-unneeded {} \;
    find "$pkgdir"/usr/${_target} -name '*.a' -o -name '*.dll' | xargs ${_target}-strip -g
    # remove unnecessary files
    rm -r "$pkgdir"/usr/${_target}/share/man/
  done
  install -Dm 644 COPYING -t "$pkgdir"/usr/share/licenses/${pkgname}
}
